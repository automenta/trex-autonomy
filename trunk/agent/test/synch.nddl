#include "TREX.nddl"

/**
 * Here is an example of a state machine where we have a circuit breaker open or closed. The example will utilize a behavior
 * and require it to be successful inorder to transition. Anytime it is open, we want it closed.
 */
class CircuitBreaker extends AgentTimeline {
  predicate Open{}
  predicate Closed{}

  CircuitBreaker(){ super(Internal, "Open"); }
};

class ResetCircuitBreaker extends Behavior {
  predicate Inactive{}
  predicate Active{}
  ResetCircuitBreaker(Mode m) { super(m); }
};

/** RULES */

CircuitBreaker::Open{
  contains_start(ResetCircuitBreaker.Active cmd);
  ends cmd;
  eq(cmd.status, SUCCESS);
  meets(Closed);
}

/**
 * Here is an example of an action that will require a sub action to end it.
 */
class MasterAction extends Behavior {
  predicate Active{}
  predicate Inactive{}

  MasterAction(Mode _mode){
    super(_mode);
  }
};

class SlaveAction extends Behavior {
  predicate Active{}
  predicate Inactive{}

  SlaveAction(Mode _mode){
    super(_mode);
  }
};


MasterAction::Active{
  leq(duration, 2);
  ends(SlaveAction.Active);
}
